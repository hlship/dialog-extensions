#pub-interior
(name *) Inside the Pub
(room *)
(* attracts #pub-door/#window)
(look *)
    The pub's interior matches your every expectation. Dark wood, a well polished bar, several deep booths.
    Despite the early hour and chill outside, the pub is empty but for the bartender endlessly
    polishing the bar. Through a low overhang, an ancient and likely structural wooden beam, you can 
    see a few flashing lights from the room to the north.
(from * go #west through #pub-door to #outside-pub)
(from * go #north to #game-room)
(narrate leaving * #north)
    You bend down quite low to squeeze under the wooden beam as you make your way into the game room.
(#bartender/#booths/#bar is #in *)
(* attracts #beam)

#beam
(name *) heavy wood beam
(descr *)
    The beam itself, though large, is non-descript.  It's position speaks to the considerable age of the pub; the game room
    almost certainly had a different purpose, originally, and was likely not connected to the pub.

    
#bar
(name *) polished bar
(supporter *)
(descr *)
    The bar is made of a lighter wood with a more visible grain to it. It's looks large enough to hold 
    drinks for the entire town.

#bartender
(male *)
(name *) bartender
(dict *) man
(descr *)
    Younger than you'd think, he wears a lightly stained apron, and has rolled up the sleeves
    of his button-down shirt.
    He never seems to stop moving, even though he also seems rooted in place.
(narrate * expressing ignorance)
    "Not sure I follow you there" he says.
(beat $)
    He looks down for a bit, working his rag over a spot that looks mirror-smooth to you.
    (par)
(narrate greeting *)
    You catch the bartender's eye, and he's eager to engage with his only customer.

#where-everyone-is
(asking quip *)
(* supplies #bartender)
(name *) where everyone is
(command word *) everyone
(comment *)
    "Hey mate," you say, 
    (if) (current room #pub-interior) 
    (then)
        approaching the bar, 
    (endif)
    "awfully quiet night in here, isn't it?"
(reply *)
    "Right you are sir!" he says, with just a note of agitation. "Been like this for too long, I say. But it's not
    like folks are going to go all dry, is it?"  He thinks for a moment. "Things will pick up, soon enough; they have to."

#noisy-machine
(telling quip *)
(name *) that the machine is quite noisy
(* supplies #bartender)
(off limits *)
    (#game-room is unvisited)
(comment *)
    (if) (current room #game-room)
    (then)
        "This thing," you say, pointing at the game machine, "
    (else)
        "That machine of yours, in the game room, 
    (endif)
    makes quite the racket, doesn't it?" you state.
(reply *)
    "Don't know that you can have a proper pub, these days, without at least one of 'em. Brings
    in quite the crowd, I reckon." He half-smiles, a bit sardonically, while glancing around the empty pub.
    (par)
    "Still, on billiards night, I have to turn it off. Too much of a distraction."
    (queue #play-billiards for #bartender as #postponed-obligatory)

#turn-noisy-machine-off
(asking quip *)
(name *) to turn it off
(* follows #noisy-machine)
(comment *)
    "I have no intention of engaging with that thing, could you bloody well pull the plug?".
(reply *)
    "Sorry, no," he replies "I like to keep tabs on the bar, unless I need to nip into the kitchen."

#play-billiards
(reply *)
    "You know," says the bartender, "I could pull out the cue and balls for you, if you'd like
    to get in some practice. We have ourselves a little tourney, first Fridays," he adds, looking 
    around the empty pub, "most months."


#not-enough-dimensions
(telling quip *)
(name *) not enough dimensions
(* follows #play-billiards)
(comment *)
    "I'm afraid your table doesn't have enough dimensions for me."
(reply *)
    "It's absolutely regulation size!" the bartender admonishes.
    (par) 
    "I'm sure it is, chap" you reply, "but a merely two dimensional field lacks interest for me."
    (par)
    The bartender looks dissapointed and confused.

 
#booths
(actor container *)
(ignored by tutorial *)
(name *) deep wooden booths
(dict *) booth
(plural *)
(descr *)
    Each booth contains a pair of well-worn benches comfortably facing in towards each other, separated by a high table.
(prevent [enter *])
    Relaxing with a pint or two seems at odds with whatever your purpose is here.

